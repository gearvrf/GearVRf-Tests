apply plugin: 'com.android.application'

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs '../../../GearVRf/GVRf/gearvrf-libs'
        }
    }
}


android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "org.gearvrf.tester"
        minSdkVersion 21
        targetSdkVersion 23
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            moduleName = "physics-tests"
            abiFilter "armeabi-v7a"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets.main {
        jniLibs.srcDir 'libs' // pre-compiled libraries
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    rootProject.name + ".apk")
        }
    }
}

project.ext.jomlVersion = "1.9.1-SNAPSHOT"

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "org.joml:joml-android:${jomlVersion}"

    if (findProject(':framework')) {
        compile project (':framework')
    } else {
        compile(name:'framework-debug', ext:'aar')
    }
    if (findProject(':backend_oculus')) {
        compile project (':backend_oculus')
    } else {
        compile(name:'backend_oculus-debug', ext:'aar')
    }
    if (findProject(':gvrf-physics')) {
        compile project (':gvrf-physics')
    } else {
        compile(name: 'gvrf-physics-debug', ext: 'aar')
    }

    androidTestCompile 'com.android.support:support-annotations:23.+'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    compile 'com.android.support:appcompat-v7:23.+'
    testCompile 'junit:junit:4.12'
    compile 'net.jodah:concurrentunit:0.4.2'
    compile project(path: ':gvr-unittestutils')
}