apply plugin: 'com.android.application'

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs '../../../GearVRf/GVRf/gearvrf-libs'
        }
    }
}


android {
    compileSdkVersion 23
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "org.gearvrf.tester"
        minSdkVersion 21
        targetSdkVersion 23
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            moduleName = "framework-tests"
            abiFilter "armeabi-v7a"
        }

        externalNativeBuild {
            cmake {
                arguments.add('-DINCLUDE_DIR='+rootProject.projectDir.absolutePath)
                arguments.add('-DPATH_TO_LIBGVRF='+projectDir.absolutePath+'/build/intermediates/exploded-aar/framework-debug/jni/')
                //difference in sign extension when reinterpret_cast-ing a pointer between
                //gcc and clang hence overriding the default toolchain to match the one used
                //by the other modules
                arguments.add('-DANDROID_TOOLCHAIN=gcc')
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets.main {
        jniLibs.srcDir 'libs' // pre-compiled libraries
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = rootProject.name + ".apk"
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    packagingOptions {
        pickFirst 'lib/*/libc++_shared.so'
        pickFirst 'lib/*/libgnustl_shared.so'
    }
}

task copyOculusSignature(type: Copy) {
    if (rootProject.hasProperty("OCULUS_SIGS_DIRECTORY")) {
        copy {
            from rootProject.property("OCULUS_SIGS_DIRECTORY")
            into 'src/main/assets'
            include 'oculussig_*'
        }
    }
}

preBuild {}.dependsOn copyOculusSignature

project.ext.jomlVersion = "1.9.1-SNAPSHOT"

dependencies {
    compile "org.joml:joml-android:${jomlVersion}"

    if (findProject(':framework')) {
        compile project (':framework')
    } else {
        compile(name:'framework-debug', ext:'aar')
    }
    if (findProject(':backend_daydream')) {
        compile project (':backend_daydream')
    } else {
        compile(name:'backend_daydream-debug', ext:'aar')
    }
    if (findProject(':backend_oculus')) {
        compile project (':backend_oculus')
    } else {
        compile(name:'backend_oculus-debug', ext:'aar')
    }

    if (findProject(':backend_monoscopic')) {
        compile project (':backend_monoscopic')
    } else {
        compile(name:'backend_monoscopic-debug', ext:'aar')
    }

    androidTestCompile 'com.android.support:support-annotations:23.+'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    compile 'net.jodah:concurrentunit:0.4.2'
    compile project(path: ':gvr-unittestutils')
}

android.applicationVariants.all { variant ->
    if (variant.getBuildType().name == "debug") {
        task "configDevice${variant.name.capitalize()}" (type: Exec){
            dependsOn variant.install

            group = 'Device Configuration'
            description = 'Grants android permissions before running the application.'

            def adb = android.getAdbExe().toString()
            def permissions = [ 'android.permission.READ_EXTERNAL_STORAGE','android.permission.WRITE_EXTERNAL_STORAGE']
            for ( permission in permissions ) {
                commandLine "$adb shell pm grant ${variant.applicationId} $permission".split(' ')
            }
        }
        variant.testVariant.connectedInstrumentTest.dependsOn "configDevice${variant.name.capitalize()}"
    }
}
